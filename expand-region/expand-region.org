#+TITLE: Er/expxpand Region

* Command =er/expand-region=
All emacers must have used this command. [[https://github.com/magnars/expand-region.el][github link]]
* =er/expand-region=
#+begin_src elisp
(require 'expand-region-core)
(require 'expand-region-custom)
(require 'er-basic-expansions)

;;;###autoload
(defun er/expand-region (arg)
  "Increase selected region by semantic units.

With prefix argument expands the region that many times.
If prefix argument is negative calls `er/contract-region'.
If prefix argument is 0 it resets point and mark to their state
before calling `er/expand-region' for the first time."
  (interactive "p")
  (if (< arg 1)
      (er/contract-region (- arg))
    (er--prepare-expanding)
    (while (>= arg 1)
      (setq arg (- arg 1))
      (when (eq 'early-exit (er--expand-region-1))
        (setq arg 0)))
    (when (and expand-region-fast-keys-enabled
               (not (memq last-command '(er/expand-region er/contract-region))))
      (er/prepare-for-more-expansions))))

(eval-after-load 'clojure-mode   '(require 'clojure-mode-expansions))
(eval-after-load 'css-mode       '(require 'css-mode-expansions))
... skip
(eval-after-load "text-mode"      '(require 'text-mode-expansions))
(eval-after-load 'cperl-mode     '(require 'cperl-mode-expansions))
(eval-after-load 'sml-mode       '(require 'sml-mode-expansions))
(eval-after-load 'enh-ruby-mode  '(require 'enh-ruby-mode-expansions))
(eval-after-load 'subword        '(require 'subword-mode-expansions))
#+end_src
This is pretty much the content from =expand-region.el=.
** function walkthrough
pretty straightforward
- =interactive "p"= the prefix arg is a number
- if prefix arg is less than 1, call =er/contract-region=
- when prefix arg is n and n =gte= 1, =er--expand-region-1= is invoked n times, unless ='early-exit= is returned when =er--expand-region-1= expanded to the whole buffer.
- if =expand-region-fast-keys-enabled= is truthy, and the previous command is not =expand-region=, some preprocess is done to ensure faster later expansions.


reference: [[https://www.gnu.org/software/emacs/manual/html_node/elisp/Interactive-Codes.html#Interactive-Codes][interactive codes]]
** all those eval-after-load
whenever a major mode is loaded, loads te corresponding =*-expansions= sub-package.

different language has different syntax-table and semantic meanings for the same char, that's why there is one =*-expansions= per language.

** =er--expand-region-1=
[[https://github.com/magnars/expand-region.el/blob/4b8322774d9c1d8b64a0049d1dbbc1e7ce80c1a0/expand-region-core.el#L78][link]]

As the docstring points out, the key part is:
#+begin_src elisp
  (let* (...
         (try-list er/try-expand-list)
         (best-start (point-min))
         (best-end (point-max))
         ...)

         ...
    (while try-list
      (er--save-excursion
       (ignore-errors
         (funcall (car try-list))
         (when (and (region-active-p)
                    (er--this-expansion-is-better start end best-start best-end))
           (setq best-start (point))
           (setq best-end (mark))
           (when (and er--show-expansion-message (not (minibufferp)))
             (message "%S" (car try-list))))))
      (setq try-list (cdr try-list)))
...
#+end_src
In above code block, it makes a copy of =er/try-expand-list= and loop through one by one, update =best-start= and =best-end=.
*** er--this-expansion-is-better
simple arithemtic computation to find the smallest expand that's larger than the starting region. You can checkout the code [[https://github.com/magnars/expand-region.el/blob/4b8322774d9c1d8b64a0049d1dbbc1e7ce80c1a0/expand-region-core.el#L135][here]].

*** er--show-expansion-message
[[https://github.com/magnars/expand-region.el/blob/4b8322774d9c1d8b64a0049d1dbbc1e7ce80c1a0/expand-region-custom.el#L118][link]]
"Whether expand-region should show usage message."

*** expand-region-smart-cursor
By =er/expand-region= convention, the =point= is always at the beginning of the expanded region and a =mark= at the end. Unless =expand-region-smart-cursor= is truthy.
#+begin_src elisp
;;;###autoload
(defcustom expand-region-smart-cursor nil
  "Defines whether the cursor should be placed intelligently after expansion.

If set to t, and the cursor is already at the beginning of the new region,
keep it there; otherwise, put it at the end of the region.

If set to nil, always place the cursor at the beginning of the region."
  :group 'expand-region
  :type '(choice (const :tag "Smart behaviour" t)
                 (const :tag "Standard behaviour" nil)))
#+end_src

*** early exit
#+begin_src elisp
    (when (and (= best-start (point-min))
               (= best-end (point-max))) ;; We didn't find anything new, so exit early
      'early-exit)
#+end_src
if the expansion is already at the max and min possible, return ='early-exit'=.

*** er/try-expand-list


* expansion and contraction history
#+begin_src elisp
    ;; add hook to clear history on buffer changes
    (unless er/history
      (add-hook 'after-change-functions 'er/clear-history t t))

    ;; remember the start and end points so we can contract later
    ;; unless we're already at maximum size
    (unless (and (= start best-start)
                 (= end best-end))
      (push (cons p1 p2) er/history))

#+end_src

 1. register at the beginning of  expansion session to clear history when buffer content change.
 2. register at each invocation of expansion the start and end of the current region so later we can easily and deterministically contract back to the same region.
* text-mode-expansions.el
let's deep-dive one of the easiest expansion: [[https://github.com/magnars/expand-region.el/blob/4b8322774d9c1d8b64a0049d1dbbc1e7ce80c1a0/text-mode-expansions.el#L56][text-mode-expansions]].

The most important line is to add to the =er/try-expand-list=
#+begin_src elisp
(defun er/add-text-mode-expansions ()
  "Adds expansions for buffers in `text-mode' except for `html-mode'.
Unfortunately `html-mode' inherits from `text-mode' and
text-mode-expansions don't work well in `html-mode'."
  (unless (member major-mode expand-region-exclude-text-mode-expansions)
    (set (make-local-variable 'er/try-expand-list)
         (append
          er/try-expand-list
          '(er/mark-text-sentence
            er/mark-text-paragraph
            mark-page)))))
#+end_src
only add =text-mode-expansions= when the current major-mode is NOT =expand-region-exclude-text-mode-expansions=

expand-region-exclude-text-mode-expansions is customizable.
#+begin_src elisp
;;;###autoload
(defcustom expand-region-exclude-text-mode-expansions
  '(html-mode nxml-mode)
  "List of modes which derive from `text-mode' for which text mode expansions are not appropriate."
  :group 'expand-region
  :type '(repeat (symbol :tag "Major Mode" unknown)))
#+end_src

set =er/try-expand-list= to buffer-local so that it doesn't affect its list value elsewhere in other buffers.

append three new =try-expand-list= methods for text-mode into =er/try-expand-list=:
1. =er/mark-text-sentence=
2. =er/mark-text-paragraph=
3. =mark-page=

#+begin_src elisp
(defun er/mark-text-sentence ()
  "Marks one sentence."
  (interactive)
  ;; The obvious
  ;; (backward-sentence 1) (mark-end-of-sentence 1)
  ;; doesn't work here because it's repeated and the selection keeps
  ;; growing by sentences, which isn't what's wanted.
  (forward-sentence 1)
  (set-mark (point))
  (backward-sentence 1))
#+end_src

goto the end of the setence, mark, then go back to the beginning of the setence.

Note repeated calls of this method doesn't grow the region. b/c the higher semantic region is a paragraph not two setences.

#+begin_src elisp
(defun er/mark-text-paragraph ()
  "Marks one paragraph."
  (interactive)
  (mark-paragraph)
  (skip-chars-forward er--space-str))

#+end_src
straightforward, =mark-paragraph= and "trim" the whitespace at the beginning of the paragraph.

and finally the higher level of semantic region is a page for text mode.
* subword-mode-expansion.el
this expansion extension covers the expanding subwords, e.g. camelCased words. It appends the =er/mark-subword= method into =er/try-expand-list=

#+begin_src elisp
(defun er/mark-subword ()
  "Mark a subword, a part of a CamelCase identifier."
  (interactive)
  (when (and subword-mode
             expand-region-subword-enabled)
    (subword-right 1)
    (set-mark (point))
    (subword-left 1)))
#+end_src
only invoke when =subword-mode= is on and =expand-region-subword-enabled= is truthy.

1. move one subword right
2. mark
3. move one subword left.

repeated calls doens't grow the region.


* contraction
=er/contract-region= is pretty much the oppsite of the =er/expand-region=, so i will not covert it this time.
